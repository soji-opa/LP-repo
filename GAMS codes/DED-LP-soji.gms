$title Linearized Dynamic Economic Load Dispatch

$onText
For more details please refer to Chapter 4 (Gcode4.7), of the following book:
Soroudi, Alireza. Power System Optimization Modeling in GAMS. Springer, 2017.
--------------------------------------------------------------------------------
Model type: LP
--------------------------------------------------------------------------------
Contributed by
Dr. Alireza Soroudi
IEEE Senior Member
Email: alireza.soroudi@gmail.com
We do request that publications derived from the use of the developed GAMS code
explicitly acknowledge that fact by citing
Soroudi, Alireza. Power System Optimization Modeling in GAMS. Springer, 2017.
DOI: doi.org/10.1007/978-3-319-62350-4

Student- Azeez Olasoji

----------------------------------------------------------------------------

The process of linearizing the cost fnc is explained in eqn 4.13b to 4.13j

Sets:

Set t 'hours' defines the time periods (hours) for which the electricity demand is considered.
Set i 'thermal units' defines the thermal power plants that are available for generating electricity.
Set k is an index set used to discretize the power output range of each thermal unit into smaller intervals.
Parameters:

Parameter demand(t) represents the demand for electricity in each time period.
Table gendata(i,*) contains cost characteristics and limits for each thermal unit.
Parameter data(k,i,*) is used to store some intermediate calculations.
Variables:

Variable OF represents the objective function value, which is the total cost of generating electricity.
Variable p(i,t) represents the power generated by each thermal unit at each time period.
Variable Pk(i,t,k) represents the power output of each thermal unit in each time period and each interval of power output.
Equations:

Equation eq1 defines the objective function as the total cost of generating electricity,
which is the sum of the fixed cost and the variable cost for each thermal unit.

Equation eq2 and eq3 represent the ramp up and ramp down constraints,
respectively, which limit the change in power output of each thermal unit between two consecutive time periods.


Equation eq4 represents the demand constraint,
which ensures that the total power generated in each time period meets the demand for electricity.


Equation eq5 relates the power generated by each thermal unit to the power output of each thermal unit in each time period and each interval of power output.
The model is then solved using linear programming (LP) to minimize the objective function.
The optimal values of the power generated by each thermal unit at each time period are displayed.
----------------------------------------------------------------------------
$offText

Set
   t 'hours'         / t1*t24 /
   i 'thermal units' / p1*p4  /
   k                 /  1*50 /;
   
*'I had to reduce the intervals to 50 because of licensing issues'

Table gendata(i,*) 'generator cost characteristics and limits'
       a     b      c    d     e     f     Pmin  Pmax  RU0  RD0
   p1  0.12  14.80  89   1.2  -5     3     28    200   40   40
   p2  0.17  16.57  83   2.3  -4.24  6.09  20    290   30   30
   p3  0.15  15.55  100  1.1  -2.15  5.69  30    190   30   30
   p4  0.19  16.21  70   1.1  -3.99  6.2   20    260   50   50;

Parameter demand(t) /  t1 510,  t2 530,  t3 516, t4  510,  t5 515,  t6 544
                       t7 646,  t8 686,  t9 741, t10 734, t11 748, t12 760
                      t13 754, t14 700, t15 686, t16 720, t17 714, t18 761
                      t19 727, t20 714, t21 618, t22 584, t23 578, t24 544 /;

Variable
   OF     'objective (revenue)'
   p(i,t) 'power generated by thermal power plant'
   Pk(i,t,k);

Parameter data(k,i,*);
data(k,i,'DP')   = (gendata(i,"Pmax") - gendata(i,"Pmin"))/card(k);
data(k,i,'Pini') = (ord(k) - 1)*data(k,i,'DP') + gendata(i,"Pmin");
data(k,i,'Pfin') =  data(k,i,'Pini') + data(k,i,'DP');
data(k,i,'Cini') =  gendata(i,"a")*power(data(k,i,'Pini'),2)
                 +  gendata(i,"b")*data(k,i,'Pini') + gendata(i,"c");
data(k,i,'Cfin') =  gendata(i,"a")*power(data(k,i,'Pfin'),2)
                 +  gendata(i,"b")*data(k,i,'Pfin') + gendata(i,"c");
data(k,i,'s')    = (data(k,i,'Cfin') - data(k,i,'Cini'))/data(k,i,'DP');

p.up(i,t)    = gendata(i,"Pmax");
p.lo(i,t)    = gendata(i,"Pmin");
Pk.up(i,t,k) = data(k,i,'DP');
Pk.lo(i,t,k) = 0;

Equation eq1, eq2, eq3, eq4, eq5;

eq1..      OF =e= sum((t,i), gendata(i,'a')*power(gendata(i,"Pmin"),2)
                           + gendata(i,'b')*gendata(i,"Pmin")
                           + gendata(i,'c') + sum(k, data(k,i,'s')*pk(i,t,k)));

eq2(i,t).. p(i,t+1) - p(i,t) =l= gendata(i,'RU0');

eq3(i,t).. p(i,t-1) - p(i,t) =l= gendata(i,'RD0');

eq4(t)..   sum(i,p(i,t))     =g= demand(t);

eq5(i,t).. p(i,t) =e= gendata(i,"Pmin") + sum(k, Pk(i,t,k));

Model DEDLP / all /;
solve DEDLP using lp minimizing OF;
display p.l
